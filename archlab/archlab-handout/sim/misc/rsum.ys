# rsum.ys
# written by fyerfyer in 2024.8.27

.pos 0

init:
    # Initialize stack pointer
    irmovq stack, %rsp

    # Set up parameters and call sum_list
    irmovq ele1, %rdi
    call rsum_list

    # Halt the program after the sum is computed
    halt

rsum_list:
    # Check if the pointer is null
    # val in %rdx, rest in %rax`
    # save the callee state first
    pushq %rbx
    irmovq $0, %rax
    andq %rdi, %rdi
    je end

    mrmovq 0(%rdi), %rbx
    mrmovq 8(%rdi), %rdi
    call rsum_list
    addq %rbx, %rax

end:
    popq %rbx
    ret

# Data section
.align 8
ele1:
    .quad 0x00a           
    .quad ele2            
ele2:
    .quad 0x0b0           
    .quad ele3            
ele3:
    .quad 0xc00           
    .quad 0               

# Stack section (grows downward)
.pos 0x100
stack:
